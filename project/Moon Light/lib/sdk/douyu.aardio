import web.json;    
import inet.http;
import crypt;
import web.script;
import hpsocket.tcpPullClient;
import win.ui;
import win.clip;

namespace sdk;
class douyu{
	ctor( url ){
		var http = ..inet.http();
		http.disableCache();
		var douyudanmu = {
				server = "openapi-danmu.douyu.com";
				port = 8601;
		};
		this.webapi = "api";
		this.socket := ..hpsocket.tcpPullClient();
		this._form := ..win.form();
		//this._form.messageOnly();
		
	}
	getRoomInfo = function(){
		var ret = {};
		try{
				 
				// 如果全部都是数字
	/*
				if( !..string.match(room, "\D") ){
					return room; 
				}
	*/
							
			// 获取直播房间详情
			var murl = ..string.replace(url, "@www", "m");
			var html = http.get(murl);
			if( !html ){
				return null, "网络连接有误";
			}
			var room = ..string.match(html, 'rid"\\:(\\d{1,7}),"vipId');
			room := ..string.match( url, "<@www.douyu.com/@>(.+?)$" ) 
			var douyuapi = "http://capi.douyucdn.cn/api/v1/room/%s?aid=android&client_sys=android&time=%s&auth=%s";
			var timestr = ..string.format("%i", tonumber(..time.now()));
			var md5rb = "room/%s?aid=android&client_sys=android&time=%s";
			md5rb = ..string.format(md5rb, room, timestr) ++ "1231"
			var authstr = ..crypt.md5(md5rb, false);
			if(this.webapi == "api"){
				html = http.get( "http://open.douyucdn.cn/api/RoomApi/room/" ++ room );
				if(!html){
					this.webapi = "html";
				}
				else {
					var r = ..web.json.parse( html )
					if( r.error !== 0 )
						return null, "信息数据解析有误";
					ret.website = "douyu";
					ret.roomUrl = url;
					ret.roomId = r.data.room_id;
					ret.ownerName = r.data.owner_name;
					ret.roomName = r.data.room_name;
					ret.avatarUrl = r.data.avatar;
					ret.room_status = r.data.room_status;
					ret.isLive = ret.room_status == "1";
				}
				
			}
	
			//..win.clip.write(html)
			if(this.webapi == "html"){
				html = http.get( url );
				if(!html){
					return null, "网络连接有误";
				}
				ret.roomId = ..string.match(html, "$ROOM\.room_id\s*=\s*([^;]+)\s*;");
				ret.room_status = ..string.match(html, "$ROOM\.show_status\s*=\s*([^;]+)\s*;");
				ret.avatarUrl = ..string.match(html, `$ROOM\.owner_avatar\s*=\s*"([^;]+)"\s*;`);
				ret.avatarUrl = ..string.unescape(ret.avatarUrl);
				var tempr = ..string.matches(html, `\<(\w+)\s+class="Title-header"\>([^<]+)\</\1\>`);
				if(#tempr){
					ret.roomName = tempr[1][2];
				}
				else {
					ret.roomName = "";
				}
				
				tempr = ..string.matches(html, `\<(\w+)\s+class="Title-anchorNameH2"[^>]*\>([^<]+)\</\1\>`);
				if(#tempr){
					ret.ownerName = tempr[1][2];
				}
				else {
					ret.ownerName = "";
				}
				
				ret.website = "douyu";
				ret.roomUrl = url;
				ret.isLive = ret.room_status == "1";
			}
		}
		catch(e){
			return null, e;
		}
		
		//return r.data.room_id ;
		return ret;
	}
	getStreamInfo1 = function( cdn = "主线路", bitrate = "超清"){	   
		var roomId = this.getRoomInfo().roomId;
		if( !roomId ){
			return null, "获取房间基本信息失败"; 
		}
		this.roomId = roomId;
							
		var key = "zNzMV1y4EMxOHS6I5WKm"
		var apiUrl = "http://capi.douyucdn.cn/api/v1/"
		
			var path = "room/" ++ roomId  ++ "?" ++ "aid=wp&client_sys=wp&time=" ++ tonumber(..time())
		var url = apiUrl ++ path ++ "&auth=" ++ ..crypt.md5( path ++ key, false ) 
		
		var html = http.get( url )
		if( !html ){
			return null, "网络连接有误"; 
		}
	
		var r = ..web.json.parse(html)
		if( r.error === 0 ){
			var data = r.data;
			var lastUrl;
			if( #data.rtmp_multi_bitrate[ bitrateMap[bitrate] ] ){
				lastUrl = data.rtmp_multi_bitrate[ bitrateMap[bitrate] ] 
			}
			else {
				lastUrl = data.rtmp_live 
			}
			
			return {
				website = "douyu";
				roomUrl = "https://www.douyu.com/" ++  roomId;
				roomId = this.roomId;
				ownerName = data.nickname;
				roomName = data.room_name;
				avatarUrl = data.owner_avatar;	
				isLive = data.show_status == "1";
				streamUrl = data.rtmp_url ++ "/" ++ lastUrl;	
			}
		} 
		return null, "地址数据解析有误"; 		
	}
	get_tt = function(){
		var tt = ..time.now(); 
		var tt2 = tostring(tt.getMilliTime());
		var tt1 = ..string.left(tt2, #tt2 - 3); 
		var today = tostring(..time(tt, "%Y%m%d"));
		return tt1, tt2, today;
	}

	get_homejs = function(rid){
		var room_baseurl = "https://m.douyu.com/";
		var response = http.get(room_baseurl ++ rid);
		var pattern = '"rid"\\:(\\d+)';
		var real_rid = rid;
		if(response){
			real_rid = ..string.match(response, pattern);
		}

		if(real_rid != rid){
			response = http.get(room_baseurl ++ real_rid);
		}
		var homejs = "";
		pattern = "(function ub9[^\r\n]*).+?(var[^\r\n]*)"
		if(response){
			var result = ..string.matches(response, pattern);
			var str1 = ..string.replace(result[1][1], "eval.*;}", "strc;}");
			homejs = str1 ++ result[1][2];
		}
		return homejs, real_rid;
	}

	get_sign = function(rid, post_v, tt, ub9){
		var sign = "";
		if(#ub9){
			var jsscript = ..web.script("JavaScript");
			jsscript.addCode(ub9);
			var str3 = jsscript.script.ub98484234();
			str3 = ..string.replace(str3, "\(function[\s\S]*toString\(\)", "'");
			var md5rb = ..crypt.md5(rid ++ '10000000000000000000000000001501' ++
							tt ++ '2501' ++ post_v, false);
			var str4 = "function get_sign(){var rb='" ++ md5rb ++ str3;
			var str5 = ..string.replace(str4, "return rt;}[\s\S]*", "return re;};");
			var str6 = ..string.replace(str5, '"v=.*&sign="\\+', "");
			jsscript.reset();
			jsscript.addCode(str6);
			sign = jsscript.xcall("get_sign", rid, '10000000000000000000000000001501', tt);
		}

		return sign;
	}

	mix_room = function(rid){
		var result1 = "PKing";
		return result1;
	}
	get_pre_key = function(rid, tt = ""){
		var request_url = "https://playweb.douyucdn.cn/lapi/live/hlsH5Preview/" ++ rid;
		var post_data = {
			rid = rid;
			did = "10000000000000000000000000001501";
		}
		var t13 = "";
		if(#tt){
			t13 = tt;
		}
		else {
			t13 = tostring(..time.now().getMilliTime());
		}
		var auth = ..crypt.md5(rid ++ t13, false);
		var header = {
			['content-type'] = 'application/x-www-form-urlencoded';
			rid = rid;
			time = t13;
			auth = auth;
		}
		var response = http.post(request_url, post_data, header);
		response ?= ..web.json.parse(response);
		var pre_key = "";
		var pre_key_flag = false; 
		if(response and response.error == 0){
			var real_url = response.data.rtmp_live;
			for(k,v in real_url){
				if(v == "mix=1"){
					pre_key_flag = true;
					break;
				}
			}
			if(pre_key_flag){
				pre_key = mix_room(rid);
			} else {
				var pattern = "^[0-9a-zA-Z]*";
				pre_key = ..string.match(real_url, pattern);
			}	
		}
		return pre_key;
	}
	get_sign_key = function(post_v, rid, tt, ub9, rate = 0){
		var sign = this.get_sign(rid, post_v, tt, ub9);
		var request_url = "https://m.douyu.com/api/room/ratestream";
		var post_data = {
			v = '2501' ++ post_v;
			did = '10000000000000000000000000001501';
			tt = tt;
			sign = sign;
			ver = '219032101';
			rid = rid;
			rate = '-1';
		};
		header = {
			['Content-Type'] = 'application/x-www-form-urlencoded';
			['User-Agent'] = 'Mozilla/5.0 (Linux; Android 5.0; SM-G900P Build/LRX21T) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/75.0.3770.100 Mobile Safari/537.36';
		};
		var response = http.post(request_url, post_data, header);
		response ?= ..web.json.parse(response);
		var result1 = "";
		var result1_flag = false; 
		if(response and response.code == 0){
			var real_url = response.data.url;
			for(k,v in real_url){
				if(v == "mix=1"){
					result1_flag = true;
					break;
				}
			}
			if(result1_flag){
				result1 = mix_room(rid);
			}
			else {
				var pattern;
				if(rate = 3){
					pattern = "/(\d{1,8}[0-9a-zA-Z]+_?[\d]{0,4}).m3u8";
				}
				else {
					pattern = "/(\d{1,8}[0-9a-zA-Z]+)_?[\d]{0,4}.m3u8";
				}
				result1 = ..string.match(real_url, pattern);
			}	
		}
		else {
			result1 = "";
		}
		return result1;
	}
	get_pc_url = function( cdn = "ws-h5", rate = 0){
/*
		通过PC网页端的接口获取完整直播源。
		:param cdn: 主线路ws-h5、备用线路tct-h5
		:param rate: 1流畅；2高清；3超清；4蓝光4M；0蓝光8M或10M
		:return: JSON格式
*/
		var murl = ..string.replace(url, "@www", "m");
		var html = http.get(murl);
		if( !html ){
			return null, "网络连接有误";
		}
		var rid = ..string.match(html, 'rid"\\:(\\d{1,7}),"vipId');
		var did = "10000000000000000000000000001501";
		html = http.get("https://www.douyu.com/" ++ rid);
		if( !html ){
			return null, "网络连接有误";
		}
		var func_ub9 = ..string.match(html, "(vdwdae325w_64we[\s\S]*function ub98484234[\s\S]*?)function");
		func_ub9 ?= ..string.replace(func_ub9, "eval.*?;}", "strc;}");
		var jsscript = ..web.script("JavaScript");
		if( !#func_ub9 ){
			return null, "无法解析数据";
		}
		jsscript.addCode(func_ub9);
		var func_str = jsscript.script.ub98484234();
		if( !#func_str ){
			return null, "无法解析数据";
		}
		var v = ..string.match(func_str, "v=(\d+)");
		var t10 = tostring(tonumber(..time()));
		var md5rb = ..crypt.md5(rid ++ did ++ t10 ++ v, false);
		var func_sign = ..string.replace(func_str, "return rt;}[\);]+", "return rt;}");
		func_sign ?= ..string.replace(func_sign, "\(function \(", "function sign\(");
		func_sign ?= ..string.replace(func_sign, "CryptoJS.MD5\(cb\).toString\(\)",
																'"' ++ md5rb ++ '"');
		jsscript.reset();
		//..win.msgbox(func_sign);
		jsscript.addCode(func_sign);
		var params = jsscript.xcall("sign", rid, did, t10);
		params = params ++ ..string.format("&cdn=%s&rate=%s", cdn, rate);
		var url = "https://www.douyu.com/lapi/live/getH5Play/%s";
		var response = http.post(..string.format(url, rid), params);
		//..win.clip.write(response);
		response = ..web.json.parse(response);
		var real_url = "";
		if(response["error"] == 0){
			real_url = response.data.rtmp_url ++ "/" ++ response.data.rtmp_live;
		}
		//..console.dump(real_url);
		return real_url;
	}
	getStreamInfo = function( cdn = "ws-h5", rate = 0){
		var data, err = this.getRoomInfo();
		if( !data ){
			return null, "获取房间基本信息失败"; 
		}
		var roomId = data.roomId;
		var real_url = "";
		try{
			if(rate == 3){
				real_url = this.get_pc_url(cdn, rate);
				if(!#real_url){
					real_url = this.get_pc_url();
				}
			}
			else {
				real_url = this.get_pc_url();
			}
			
			if(!#real_url){
				this.roomId = roomId;
				var rid = roomId;
				var tt = {this.get_tt()};
				var key = this.get_pre_key(rid, tt[2]);
				if(!#key){
					var homejs, real_rid = this.get_homejs(rid);
					key = this.get_sign_key(tt[3], real_rid, tt[1], homejs, rate);
					if(!#key and data.room_status == "1"){
						return null, "地址数据解析有误";
					}
				}
				real_url = "http://dyscdnali1.douyucdn.cn/live/" ++ key ++ ".flv?uuid=";
			}
		}
		catch(e){
			return null, e; 
		}

		data.streamUrl = real_url;
		return data;

	}

	sendData = function(s){
		var data = {
			int len;
			struct msg = {
				int len;
				word type = 689;
				byte cryptedField;
				byte reservedField;
				byte body[] = s;
				byte e;
			};
		};
	
		data.len = ..raw.sizeof(data.msg);
		data.msg.len = data.len;
		return this.socket.send( data,..raw.sizeof(data ));
	}
	socketStop = function(){
		this.logout();
		this.socket.stop();
		//this._form.close();	
	}

	connect = function(roomId,host="openbarrage.douyutv.com",port=8601){

		//var tcpPullClient = ..hpsocket.tcpPullClient(); 
		this.socket.onPullReceive = function(hpTcpPullClient,connId,length){
			import thread.command;
			import sdk.douyu;
			
			while( 
				var min, remain = 9, length;
				;
				remain >= min
			) {
				remain -= 4;
				var buf = {INT len};
				if 0 !== hpTcpPullClient.peek( buf, 4 )
					return ;
						
				var len = buf.len;
				if remain < len 
					return ;
						
				remain -= len;
				var bufLen = 4 + len;
				buf = raw.buffer( bufLen );
				if 0 !== hpTcpPullClient.fetch( buf, bufLen )
					return ;
				var s = raw.tostring(buf, 13);
				if( string.match( s, `type@=(.+?)/`) ){
					var nickname = string.match( s, `nn@=(.+?)/`);
					var txt = string.match( s, `txt@=(.+?)/`);
					var roomid = string.match( s, `rid@=(.+?)/`);
					s = sdk.douyu.parse(s);
					var sdkname = sdk.douyu.info.name;
					if( nickname ? txt ){
						thread.command.post("pushdanmu", sdkname, s, roomid, nickname, txt);
					} else {
						thread.command.post("pushdanmu", sdkname, s);
					}	
				}
			}
		}
		this.socket.stop();
		if( !this.socket.start( host,port,false) ){
			return null, "连接斗鱼弹幕服务器失败"; 
		}
		var loginreq = function(roomId){
			this.sendData( "type@=loginreq/roomid@=" ++ roomId ++ "/" );
		}
		var joingroup = function(roomId){
			this.sendData( "type@=joingroup/rid@=" ++ roomId ++ "/gid@=-9999/" );
		}
		var keepLive = function(){
			this.sendData( "type@=mrkl/" );
		}
		loginreq(roomId);
		joingroup(roomId);
		keepLive();

	}
	
	logout = function(){
		this.sendData( "type@=logout/" );
	}
	
	get_danmu = function(){
		import console;
		var data, err = this.getRoomInfo();
		if(!data){
			return ; 
		}
		var danmucommand = ..thread.command(this._form);
		danmucommand.stopdanmu = function(){
			this.socketStop();
			..thread.stop();
		}
		this.connect(data.roomId, douyudanmu.server, douyudanmu.port);
		this.keepliveid := this._form.addtimer( 
				30000, function(hwnd,msg,id,tick){
					import thread.table;
					var thrdTable = thread.table("weblayout");
					if(this.socket.getState() != 1){
						this.socket.stop();
						this.socket.start(
									douyudanmu.server, douyudanmu.port, false);
						this.sendData( "type@=loginreq/roomid@=" ++
										data.roomId ++ "/" );
						this.sendData( "type@=joingroup/rid@=" ++
										data.roomId ++ "/gid@=-9999/" );
					}
					//..console.dump(thrdTable.current, ..thread.getId());
					this.sendData( "type@=mrkl/" );
/*
					if(thrdTable.current != ..thread.getId()){
						this.socketStop();
						..thread.stop();
					} else {
						this.sendData( "type@=mrkl/" );
					}
*/
					
				} 
		);
		..win.loopMessage();
	}
	
}
namespace douyu{
	info = {
		name = "斗鱼";
		url = "https://www.douyu.com/";
	}
	
	
		canHandle = function(url){
		return ..string.match( url, "<@www.douyu.com/@>(.+?)$" ) 	
	} 
	cdnNames = {
		"主线路";"备用线路5";"备用线路2"
	}
	bitrate = {
		"流畅";"高清";"超清"
	}
	cdnMap = {
		主线路 = "ws";
		备用线路5 =  "tct";
		备用线路2 = "ws2";    
	}
	bitrateMap = {
		流畅 = "middle";
		高清 = "middle2";
		超清 = "rtmp_url";
	}
	escape = function(s){
		s = ..string.replace(s, "\@","@A")
		return ..string.replace(s, "\/","@S"); 
	};
	
	unescape = function(s){
		s = ..string.replace(s,"\@S", "/");
		return ..string.replace(s,"\@A", "@");		
	};
	
	stringify = function(param){ 
		if( type(param) != "table" ) return param;
		
		var lst = {};
		if(#param){
			for(i=1;#param;1){
				..table.push( lst,escape(param[i]));
			} 	
		}
		else{
			for(k,v in ..table.eachName(param) ){
				..table.push( lst,escape(k) + "@=" + ( escape(tostring(v)) ) )
			}
		}
		return ..string.join(lst, '/') + "/"; 
	};
	
	parse = function(param){ 
		if(!param) return null;
		
		var list,k,v = {};
		for line in ..string.lines(param,"/") { 
			var nv = ..string.split(line,"<@=>",2);
			if(#nv==2) {
				list[ unescape(nv[1]) ] = unescape( nv[2] );
			}
			else {
				..table.push(list,unescape(nv[1]));
			}
			
		}
		return list;
	};	     	
}

/*
import console;
console.open()
console.dump(douyu("https://www.douyu.com/610588").getStreamInfo(, 3));
execute("pause")
*/