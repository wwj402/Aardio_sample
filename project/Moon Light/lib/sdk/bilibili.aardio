import web.rest.jsonLiteClient;

namespace sdk;
class bilibili{
	ctor(url){  
		this.roomUrl = url
		var jsonLiteClient = ..web.rest.jsonLiteClient("Mozilla/5.0 (Windows NT 10.0; WOW64; rv:56.0) Gecko/20100101 Firefox/56.0");
	};
	getRoomInfo = function(){
		var retd;
		try{
			var room = 	..string.match( url, "<@live.bilibili.com/@>(\d+)");
			var url = "https://api.live.bilibili.com/room/v1/Room/room_init?id=" ++ room;
			var ret = jsonLiteClient.get( url )
			if( ret[["code"]] !== 0 ){
				return null, "获取房间信息失败"; 
			}
			this.roomId = ret.data.room_id;
			this.isLive = ret.data.live_status;
			var ownerInfo = jsonLiteClient.get( "https://api.live.bilibili.com/live_user/v1/UserInfo/get_anchor_in_room?roomid=" ++ this.roomId )
			if( ownerInfo[["code"]] !== 0){
				return null, "查询直播间信息出错"; 
			}
			var room_info_api = "https://api.live.bilibili.com/xlive/web-room/v1/index/getInfoByRoom?room_id=%s"
			var roomInfo = jsonLiteClient.get(..string.format(room_info_api, this.roomId));
			if( roomInfo[["code"]] !== 0){
				return null, "查询直播间信息出错";
			}
			retd = {
				website = "bilibili";
				roomUrl = this.roomUrl;
				roomId = this.roomId;
				ownerName = ownerInfo.data.info.uname;
				roomName = roomInfo.data.room_info.title; 
				avatarUrl = ownerInfo.data.info.face;
				isLive = this.isLive == 1; // 1 = 直播  2 = 轮播
			};
		}
		catch(e){
			return null, e;
		}
		
		return retd;
	}
	
	getStreamInfo = function(){
		var data, err = this.getRoomInfo();
		if( !data ){
			return null, "获取房间基本信息失败"; 
		}
		try{
			if( data.isLive ){
				var room_url_api = "https://api.live.bilibili.com/xlive/web-room/v1/index/getRoomPlayInfo?room_id=%s&play_url=1&mask=1&qn=%s&platform=web"; 
				var qn = 1;//PC网页和手机APP端的qn=1是最高画质;qn取值0~4。
				var streamInfo = jsonLiteClient.get(..string.format(room_url_api, this.roomId, qn));
				//var streamInfo = jsonLiteClient.get( "https://api.live.bilibili.com/room/v1/Room/playUrl?cid=" ++ this.roomId ++ "&quality=0&platform=web" )
				if( streamInfo[["code"]] !== 0){
					return null, "查询直播视频地址失败"; 
				}
				
				//ret.streamUrl = streamInfo.data.durl[1].url;
				var url_infos = streamInfo.data.play_url.durl;
				data.streamUrl = url_infos[#url_infos].url;
			}
		}
		catch(e){
			return null, e; 
		}
		return data;
	}	
}
namespace bilibili{
    info = {
        name = "哔哩哔哩";
        url = "https://live.bilibili.com/";
    }
    
    canHandle = function(url){
    	return ..string.match( url, "<@live.bilibili.com/@>\d+"); 
    }      	
}

/*
..io.open()
..io.print(bilibili("https://live.bilibili.com/14047").getStreamInfo())
execute("pause")
*/