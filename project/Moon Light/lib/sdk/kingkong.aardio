import inet.http;
import string.html;
import string.xml;
import web.json;

namespace sdk;
class kingkong{
	ctor(url){
		var http = ..inet.http();
		var roomId = ..string.match( url, "<@www.kingkong.com.tw/@>(\d+)");
		var roomUrl = "https://www.kingkong.com.tw/"  ++ roomId;
	};
	getStreamInfo = function(){
		var html, err = http.get( roomUrl ) // ..io.print( html, err )
		if( !html ){
			return null, "网络连接有误"; 
		}
		var xml = ..string.xml(html)
		var ele = xml.queryEle( ["class"]="view-live" )
		if( !ele ){
			return null, "数据解析有误"; 
		}
		var roomInfo = ele["data-roomInfo"]
		roomInfo = ..string.html.toText(roomInfo)
		roomInfo = ..web.json.parse(roomInfo)
		
		var ret = {
			website = "kingkong";
			roomUrl = roomUrl;
			roomId = roomId;
			avatarUrl = roomInfo.avatar;
			ownerName = roomInfo.name;
			roomName = roomInfo.roomTitle;
			isLive = roomInfo.liveStatus == 1;
			
		} 
		if( ret.isLive ){
			ret.streamUrl = roomInfo.videos[1].url;
		}

		return ret; 
	}
	getRoomInfo = function(){
		return this.getStreamInfo();
	}
}
namespace kingkong{
    info = {
        name = "金刚";
        url = "https://www.kingkong.com.tw/";
    }
    
	canHandle = function(url){
		return ..string.match( url, "<@www.kingkong.com.tw/@>\d+" ) 
	}	        	
}